// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant 
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // Put you code here:

   //Selection for x (with zx and nx flag)
   Mux16(a=x, b[0..15]=false, sel=zx, out=k1);
   Not16(in=k1, out=k2);
   Mux16(a=k1, b=k2, sel=nx, out=k3);

   //Selection for y (with zy and ny flag)
   Mux16(a=y, b[0..15]=false, sel=zy, out=k4);
   Not16(in=k4, out=k5);
   Mux16(a=k4, b=k5, sel=ny, out=k6);

   //Calculate x combine with y (x+y or x&y)
   Add16(a=k3, b=k6, out=k7);
   And16(a=k3, b=k6, out=k8);
   Mux16(a=k8, b=k7, sel=f, out=k9);
   Not16(in=k9, out=k10);
   
   //Must divide the bus into sub-buses, can't use the sub-buses without breaking them first.
   Mux16(a=k9, b=k10, sel=no, out[15]=k11, out[0..7]=k12, out[8..15]=k13, out=k14);

   //Bring the output
   And(a=true, b=k11, out=ng);
   Or8Way(in=k12, out=k15);
   Or8Way(in=k13, out=k16);
   Or(a=k15, b=k16, out=k17);
   Not(in=k17, out=zr);
   And16(a[0..15]=true, b=k14, out=out);
}